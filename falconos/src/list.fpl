class List<T>
    var length = 0
    var array = new Array<T>(8)

    fun add(item: T)
        if length >= array.length
            var oldArray = array
            array = new Array<T>(array.length * 2)
            for i in 0..<oldArray.length
                array[i] = oldArray[i]
            free oldArray
        array[length] = item
        length+=1

    fun get(index: Int) -> T
        return array[index]

    fun set(index:Int, item: T)
        array[index] = item

    fun isEmpty() -> Bool
        return length = 0

    fun isNotEmpty() -> Bool
        return length > 0

    fun take() -> T
        if length = 0
            abort ABORT_INVALID_ARGUMENT
        length -= 1
        return array[length]

    fun clear()
        length = 0

    fun removeAt(index: Int) -> T
        var ret = array[index]
        for i in index..<length-1
            array[i] = array[i+1]
        length-=1
        return ret

    fun remove(item:T) -> Bool
        for i in 0..<length
            if array[i] = item
                for j in i..<length-1
                    array[j] = array[j+1]
                length-=1
                return true
        return false
    
    fun removeRange(startIndex:Int, endIndex:Int)
        val count = endIndex - startIndex + 1
        if startIndex<0 or count<0 or endIndex>=length
            abort ABORT_INVALID_ARGUMENT
        for i in startIndex..<length-count
            array[i] = array[i+count]
        length -= count

    fun indexOf(item:T) -> Int
        for i in 0..<length
            if array[i] = item
                return i
        return -1

    fun addAt(index: Int, item: T)
        if length >= array.length
            var oldArray = array
            array = new Array<T>(array.length * 2)
            for i in 0..<oldArray.length
                array[i] = oldArray[i]
            free oldArray
        for i in length-1..>=index
            array[i+1] = array[i]
        array[index] = item
        length+=1

    fun addAll(other: List<T>)
        for i in 0..<other.length
            add(other.array[i])

    fun contains(item:T) -> Bool
        for i in 0..<length
            if array[i] = item
                return true
        return false

    fun last() -> T?
        if length = 0
            return null
        return array[length-1]

    fun first() -> T?
        if length = 0
            return null
        return array[0]

    fun free()
        free array